//
// Second stage postprocessing (overlap removal)
//

#pragma kernel Postprocess2

#include "Common.hlsl"

// Input
ConsumeStructuredBuffer<Detection> Input;
ByteAddressBuffer Count;
float Threshold;

// Output
AppendStructuredBuffer<Detection> Output;

// Local arrays for data cache
groupshared Detection _detections[MAX_DETECTION];
groupshared bool _flags[MAX_DETECTION];

[numthreads(1, 1, 1)]
void Postprocess2(uint3 id : SV_DispatchThreadID)
{
    // Initialize data cache arrays
    uint entry_count = Count.Load(0);
    if (entry_count == 0) return;

    for (uint i = 0; i < entry_count; i++)
    {
        _detections[i] = Input.Consume();
        _flags[i] = true;
    }

    // Overlap test permutation
    for (i = 0; i < entry_count - 1; i++)
    {
        if (!_flags[i]) continue;

        for (uint j = i + 1; j < entry_count; j++)
        {
            if (!_flags[j]) continue;

            // Overlap test
            if (CalculateIOU(_detections[i], _detections[j]) < Threshold)
                continue;

            // Score comparison
            if (_detections[i].score < _detections[j].score)
            {
                _flags[i] = false;
                // The detection in the outer loop is removed. Break the inner loop.
                break;
            }
            else
                _flags[j] = false;
        }
    }

    // Output aggregation
    for (i = 0; i < entry_count; i++)
        if (_flags[i]) Output.Append(_detections[i]);
}
